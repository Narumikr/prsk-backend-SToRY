plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Coming of age SToRY with Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    runtimeOnly 'org.postgresql:postgresql'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation("org.mockito:mockito-core")
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly("com.h2database:h2")
}

tasks.named("test") {
    useJUnitPlatform()

    jvmArgs '-javaagent:' + configurations.testRuntimeClasspath.find {
        it.name.contains('byte-buddy-agent')
    }

    finalizedBy("openReport")
}

tasks.register("openReport") {
    doLast {
        // CI環境(GitHub Actions等)ではブラウザ表示をスキップ
        if (System.getenv("CI") == "true") {
            println "CI environment detected. Skipping browser opening."
            return
        }

        def reportPath = "${project.buildDir}/reports/tests/test/index.html"
        def reportFile = new File(reportPath)

        if (!reportFile.exists()) {
            println "Report not found: $reportPath"
            return
        }

        println "Opening: $reportPath"

        try {
            if (System.properties['os.name'].toLowerCase().contains('windows')) {
                exec { commandLine 'cmd', '/c', "start", reportPath }
            } else if (System.properties['os.name'].toLowerCase().contains('mac')) {
                exec { commandLine 'open', reportPath }
            } else {
                exec { commandLine 'xdg-open', reportPath }
            }
        } catch (Exception e) {
            println "Failed to open browser: ${e.message}"
            println "You can manually open the report at: $reportPath"
        }
    }
}